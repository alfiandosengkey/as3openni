<?xml version="1.0" encoding="utf-8"?>
<mx:WindowedApplication xmlns:fx="http://ns.adobe.com/mxml/2009" 
						xmlns:mx="library://ns.adobe.com/flex/mx" 
						layout="absolute"
						horizontalAlign="left"
						verticalAlign="top"
						addedToStage="init(event)"
						minWidth="800" minHeight="600"
						frameRate="180">
	<fx:Script>
		<![CDATA[
			import org.as3openni.AS3OpenNI;
			import org.as3openni.events.AS3OpenNIEvent;
			import org.as3openni.events.OpenNIEvent;
			import org.as3openni.global.Definitions;
			import org.as3openni.util.BitmapUtil;
			
			private var _flexUIOnly:Boolean = false;
			private var _as3OpenNI:AS3OpenNI;
			private var _videoCanvas:BitmapData = new BitmapData(Definitions.VGA_MAX_WIDTH, Definitions.VGA_MAX_HEIGHT, false, 0);
			private var _depthCanvas:BitmapData = new BitmapData(Definitions.VGA_MAX_WIDTH, Definitions.VGA_MAX_HEIGHT, false, 0x00FF00);
			
			[Bindable]
			private var _videoBmp:Bitmap;
			
			[Bindable]
			private var _depthBmp:Bitmap;
			
			protected function init(event:Event):void
			{
				this._videoBmp = new Bitmap(this._videoCanvas);
				this._depthBmp = new Bitmap(this._depthCanvas);
				
				if(!this._flexUIOnly)
				{
					this._as3OpenNI = new AS3OpenNI();
					this._as3OpenNI.addEventListener(AS3OpenNIEvent.READY, onReady);
					this._as3OpenNI.binaryPath = 'bin/AS3OpenNI-Bridge-v1.0.0b';
					this._as3OpenNI.debug = true;
					this._as3OpenNI.video = true;
					this._as3OpenNI.depthMap = true;
					this._as3OpenNI.depthMapBackground = false;
					this._as3OpenNI.mirrorModeOff = false;
					this._as3OpenNI.init();
				}
			}
			
			protected function onReady(event:AS3OpenNIEvent):void
			{
				this.addEventListener(Event.ENTER_FRAME, onRender);
				this._as3OpenNI.removeEventListener(AS3OpenNIEvent.READY, onReady);
				
				this._as3OpenNI.addEventListener(OpenNIEvent.ON_VIDEO, onVideo);
				this._as3OpenNI.addEventListener(OpenNIEvent.ON_DEPTH, onDepth);
			}
			
			protected function onRender(event:Event):void
			{
				if(this._as3OpenNI.isReady())
				{
					this._as3OpenNI.getVideoBuffer();
					this._as3OpenNI.getDepthBuffer();
				}
			}
			
			protected function onVideo(event:OpenNIEvent):void
			{
				var buff:ByteArray = event.data as ByteArray;
				BitmapUtil.byteArrayToBitmapData(buff, this._videoCanvas);
				//BitmapUtil.setBlackWhiteFilter(this._videoCanvas);
			}
			
			protected function onDepth(event:OpenNIEvent):void
			{
				var buff:ByteArray = event.data as ByteArray;
				BitmapUtil.byteArrayToBitmapData(buff, this._depthCanvas);
			}
			
		]]>
	</fx:Script>
	
	<mx:HBox>
		<mx:Image source="{this._videoBmp}" width="320" height="240"/>
		<mx:Image source="{this._depthBmp}" width="320" height="240"/>
	</mx:HBox>
	
</mx:WindowedApplication>
